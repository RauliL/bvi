CMAKE_MINIMUM_REQUIRED(VERSION 3.0)
PROJECT(bvi C)

OPTION(
  COMPILE_BMORE
  "Whether to also compile bmore binary or not."
  ON
)

INCLUDE(FindCurses)
INCLUDE(CheckIncludeFile)

IF(CURSES_HAVE_NCURSES_H)
  SET(HAVE_NCURSES_H 1)
ENDIF()
IF(CURSES_HAVE_CURSES_H)
  SET(HAVE_CURSES_H 1)
ENDIF()

CHECK_INCLUDE_FILE(fcntl.h HAVE_FCNTL_H)
CHECK_INCLUDE_FILE(unistd.h HAVE_UNISTD_H)

CONFIGURE_FILE(
  ${CMAKE_CURRENT_SOURCE_DIR}/config.h.in
  ${CMAKE_CURRENT_SOURCE_DIR}/config.h
)

ADD_EXECUTABLE(
  bvi
  bvi.c
  comm.c
  edit.c
  io.c
  re.c
  recomp.c
  set.c
)
TARGET_INCLUDE_DIRECTORIES(bvi PRIVATE ${CURSES_INCLUDE_DIRS})
TARGET_LINK_LIBRARIES(bvi ${CURSES_LIBRARIES})
INSTALL(TARGETS bvi RUNTIME DESTINATION bin)
INSTALL(FILES bvi.1 DESTINATION ${CMAKE_INSTALL_MANDIR}/man1 COMPONENT doc)

IF(COMPILE_BMORE)
  ADD_EXECUTABLE(
    bmore
    bmore.c
    bm_unix.c
    recomp.c
  )
  TARGET_INCLUDE_DIRECTORIES(bmore PRIVATE ${CURSES_INCLUDE_DIRS})
  TARGET_LINK_LIBRARIES(bmore ${CURSES_LIBRARIES})
  INSTALL(TARGETS bmore RUNTIME DESTINATION bin)
  INSTALL(FILES bmore.1 DESTINATION ${CMAKE_INSTALL_MANDIR}/man1 COMPONENT doc)
ENDIF()
